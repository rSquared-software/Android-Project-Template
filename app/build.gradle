apply plugin: 'com.android.application'

apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: 'utils.gradle'

def prodVersion = 1
def devVersion = 1

def prodVersionName = "1.0.0"
def devVersionName = prodVersionName + "-snap" + devVersion

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    flavorDimensions "default"

    defaultConfig {
        applicationId "software.rsquared.template"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }
    signingConfigs {
        release {
            readLocalProperties()
            try {
                storeFile file(project.KEYSTORE_DIR)
                storePassword project.KEYSTORE_PASSWORD
                keyAlias project.ALIAS
                keyPassword project.KEY_PASSWORD
            } catch (ignored) {
                storeFile debug.storeFile
                storePassword debug.storePassword
                keyAlias debug.keyAlias
                keyPassword debug.keyPassword
                gradle.taskGraph.whenReady { taskGraph ->
                    println('Warning: Invalid configuration for realease signing.' + project.KEYSTORE_DIR)
                }
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        dev {
            versionCode devVersion
            versionName devVersionName
            applicationId "software.rsquared.template.dev"
        }
        prod {
            versionCode prodVersion
            versionName prodVersionName
            applicationId "software.rsquared.template"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
}

manageApk("TemplateApp")

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.android.support:multidex:1.0.3'

    // dagger
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.google.dagger:dagger:$dagger_version"

    // dagger android
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"

    implementation "android.arch.lifecycle:extensions:1.1.1"
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
